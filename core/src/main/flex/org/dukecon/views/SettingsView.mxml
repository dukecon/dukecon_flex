<?xml version="1.0"?>
<!--
  Created by christoferdutz on 14.07.15.
-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:parsley="http://www.spicefactory.org/parsley"
        title="{resourceManager.getString('dukecon', 'settings.title')}"
        creationComplete="onCreationComplete(event)">

    <fx:Metadata>
        [ResourceBundle("dukecon")]
    </fx:Metadata>

    <fx:Declarations>
        <parsley:FastInject>
            <parsley:Inject property="conferenceController" type="{ConferenceController}" />
            <parsley:Inject property="settingsController" type="{SettingsController}" />
            <parsley:Inject property="userPreferenceController" type="{UserPreferenceController}" />
        </parsley:FastInject>
    </fx:Declarations>

    <s:navigationContent>
        <s:Button label="&lt;" click="onBackClicked(event)"/>
    </s:navigationContent>

    <fx:Script>
        <![CDATA[

        import mx.collections.ArrayCollection;
        import mx.events.FlexEvent;
        import mx.logging.ILogger;
        import mx.logging.Log;
        import mx.managers.PopUpManager;
        import mx.rpc.AsyncToken;
        import mx.rpc.events.ResultEvent;

        import org.dukecon.controller.ConferenceController;
        import org.dukecon.controller.SettingsController;
        import org.dukecon.controller.UserPreferenceController;
        import org.dukecon.dialogs.SelectDialog;
        import org.dukecon.dialogs.YesNoDialog;
        import org.jboss.keycloak.flex.event.ProviderChangedEvent;

        import spark.components.RadioButton;
        import spark.events.PopUpEvent;

        protected static var log:ILogger = Log.getLogger(getQualifiedClassName(SettingsView).replace("::", "."));

        [Inject]
        public var conferenceController:ConferenceController;

        [Inject]
        public var settingsController:SettingsController;

        [Inject]
        public var userPreferenceController:UserPreferenceController;

        protected var languageButtons:Array;

        protected function onCreationComplete(event:FlexEvent):void {
            // Hide the settings action button.
            for each(var button:Button in navigator.actionContent) {
                if (button.name == "settingsButton") {
                    button.visible = false;
                }
            }

            languageButtons = [];
            for each(var language:String in ["de_DE", "en_US"]) {
                var languageButton:RadioButton = new RadioButton();
                languageButton.label = resourceManager.getString('dukecon', 'language.' + language);
                languageButton.value = language;
                languageButton.selected = (language == resourceManager.localeChain[0]);
                languageButton.addEventListener("change", onLanguageChange);
                languageSelection.addElement(languageButton);
                languageButtons.push(languageButton);
            }

            userPreferenceController.addEventListener(ProviderChangedEvent.PROVIDER_CHANGED,
                    function (event:ProviderChangedEvent):void {
                        updateBinding();
                    });

            updateBinding();
        }

        override protected function commitProperties():void {
            super.commitProperties();

            // Select the currently selected locale button.
            for each(var languageButton:RadioButton in languageButtons) {
                languageButton.selected = (languageButton.value == resourceManager.localeChain[0]);
            }
        }

        protected function onBackClicked(event:MouseEvent):void {
            // Un-hide the settings action button.
            for each(var button:Button in navigator.actionContent) {
                if (button.name == "settingsButton") {
                    button.visible = true;
                }
            }
            navigator.popView()
        }

        protected function onLanguageChange(event:Event):void {
            var selectedLanguage:String = String(RadioButton(event.target).value);

            resourceManager.localeChain = [selectedLanguage];
            settingsController.selectedLanguage = String(selectedLanguage);

            log.info("Changed language to: " + selectedLanguage);
        }

        protected function updateBinding():void {
            // Hide the button that currently should not be displayed.
            if (userPreferenceController.provider) {
                var msg:String = resourceManager.getString('dukecon', 'settings.server-account.connected',
                        [userPreferenceController.provider]);
                boundMessage.text = msg;
                connectForm.includeInLayout = connectForm.visible = false;
                disconnectForm.includeInLayout = disconnectForm.visible = true;
            } else {
                connectForm.includeInLayout = connectForm.visible = true;
                disconnectForm.includeInLayout = disconnectForm.visible = false;
            }
        }

        protected function onBindClicked(event:MouseEvent):void {
            var localPreferences:Array = userPreferenceController.userPreferences.source;
            // TODO: If the readUserPreferences returns values, ask the user how to merge the data.
            var token:AsyncToken = userPreferenceController.readUserPreferences(navigator);
            token.addEventListener(ResultEvent.RESULT, function (event:ResultEvent):void {
                var serverPreferences:Array = (event.result as Array);
                // If the server and the client had preferences, then ask the user which
                // strategy should be used to merge the two:
                // a) delete all local preferences and use the ones read from the server
                // b) delete all remote preferences and the the local ones
                // c) merge both the local and the remote preferences and update local and
                //    remote
                if((serverPreferences.length > 0) && (localPreferences.length > 0)) {
                    openMergeStrategyDialog();
                }
                if(serverPreferences.length > 0) {
                    // TODO: Update the local preferences with the ones from the server.
                }
                else if(localPreferences.length > 0) {
                    // TODO: Send the list of preferences to the server
                }
            });
        }

        protected function openMergeStrategyDialog():void {
            var mergeStrategyDialog:SelectDialog = new SelectDialog();
            mergeStrategyDialog.message = resourceManager.getString(
                    'dukecon', 'settings.server-account.select-preferences-merge-strategy');
            mergeStrategyDialog.optionsResourcePrefix =
                    "settings.server-account.select-preferences-merge-strategy.";
            var options:ArrayCollection = new ArrayCollection();
            options.addItem("server");
            options.addItem("client");
            options.addItem("merge");
            mergeStrategyDialog.options = options;
            mergeStrategyDialog.addEventListener(PopUpEvent.CLOSE, onMergeStrategySelected, false, 0, true);
            mergeStrategyDialog.open(this, true);
            PopUpManager.centerPopUp(mergeStrategyDialog);
        }

        protected function onMergeStrategySelected(event:PopUpEvent):void {
            if (event.data == SelectDialog.OK) {
                // TODO: Implement the action of consolidating the data on server and client.
            }
        }

        protected function onUnbindClicked(event:MouseEvent):void {
            var yesNoDialog:YesNoDialog = new YesNoDialog();
            yesNoDialog.message = resourceManager.getString('dukecon', 'settings.server-account.confirm-unbind');
            yesNoDialog.addEventListener(PopUpEvent.CLOSE, onUnbindConfirmDialogClosed, false, 0, true);
            yesNoDialog.open(this, true);
            PopUpManager.centerPopUp(yesNoDialog);
        }

        protected function onUnbindConfirmDialogClosed(event:PopUpEvent):void {
            if (event.data == YesNoDialog.YES) {
                userPreferenceController.reset();
            }
        }

        ]]>
    </fx:Script>

    <s:Form width="100%">
        <s:FormItem id="languageSelection"
                    label="{resourceManager.getString('dukecon', 'settings.language')}:"
                    width="300"/>
        <s:FormItem id="connectForm" label="{resourceManager.getString('dukecon', 'settings.server-account')}:" width="300">
            <s:Button id="bindButton"
                      label="{resourceManager.getString('dukecon', 'settings.server-account.bind')}"
                      click="onBindClicked(event)" width="100%"/>
        </s:FormItem>
        <s:FormItem id="disconnectForm"
                    label="{resourceManager.getString('dukecon', 'settings.server-account')}:" width="300">
            <s:VGroup width="100%">
                <s:Label id="boundMessage" paddingBottom="20"/>
                <s:Button id="unbindButton"
                          label="{resourceManager.getString('dukecon', 'settings.server-account.unbind')}"
                          click="onUnbindClicked(event)" width="100%"/>
            </s:VGroup>
        </s:FormItem>
    </s:Form>

</s:View>
